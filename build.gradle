apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'jetty'

repositories {
    mavenCentral()
    mavenLocal()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.0.5',
            'org.springframework:spring-webmvc:3.2.4.RELEASE',
            'com.fasterxml.jackson.core:jackson-core:2.3.1',
            'com.fasterxml.jackson.datatype:jackson-datatype-joda:2.3.1',
            'com.warrenstrange:googleauth:0.2-SNAPSHOT'

    testCompile 'junit:junit:4.11',
            'org.hamcrest:hamcrest-library:1.3'

    providedRuntime "com.h2database:h2:$h2Version",
                    "commons-dbcp:commons-dbcp:1.4"

    providedCompile 'javax.servlet:javax.servlet-api:3.1.0'
}

/**
 * Build script depedency on h2 so that
 * we can initialize the H2 server on startup
 */
buildscript {
    repositories {
        mavenCentral()
        mavenLocal()
    }
    dependencies {
        classpath "com.h2database:h2:$h2Version"
    }
}

/**
 * Configure jettyRun, jettyRunWar and jettyStop tasks.
 */
[jettyRun, jettyRunWar, jettyStop]*.with {
    jettyEnvXml = file('src/test/resources/jetty-env.xml')
    webDefaultXml = file("src/test/resources/jetty-webdefault.xml")
    httpPort = 8080
    stopPort = 19742
}

/**
 * Run the h2 server before jetty starts up
 */
[jettyRun, jettyRunWar]*.doFirst {
    System.setProperty("user.timezone", "GMT")
    org.h2.tools.Server.main(['-tcp', '-pg'] as String[])
}
